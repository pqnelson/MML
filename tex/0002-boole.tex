\section{Boolean Properties of Sets: Definitions}

Originally, this was the article for identifier \verb#BOOLE# which was
refactored into \verb#XBOOLE_0# and \verb#XBOOLE_1#. In MML version
\verb#7.10.01_4.111.1036#, the definition of the empty set $\emptyset$
changed by introducing the empty adjective.

\bigbreak

\begin{paracol}{2}
        \sloppy
For simplicity
we adopt the following convention: $x$ will denote an object; $X$, $Y$,
$Z$, $V$ will have denote sets.


\switchcolumn

\begin{mizar}
reserve X, Y, Z for set,
  x, y, z for object;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

The scheme \emph{Separation}\index{Separation Scheme} concerns a constant $\mathcal{A}$ that has
the type set and a unary predicate $\mathcal{P}$ and states that the
following holds
\begin{scheme+}
  \ex X \st\ \forall x\\
  \holds x\in X\iff x\in\mathcal{A}\land\mathcal{P}[x]
\end{scheme+}
for all values of the parameters.

\switchcolumn

\begin{mizar}
scheme :: XBOOLE_0:sch 1
  Separation { A()-> set, P[object] } :
  ex X being set st
  for x
  holds x in X iff x in A() & P[x];      
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

We say that a set $X$ is \textit{empty}\index{Empty (Set)} if and only if
\begin{definition}
\neg\ \ex x \st x\in X.
\end{definition}
%\[\neg\ \ex x \st x\in X.\]

\switchcolumn

\begin{mizar}
definition
  let X be set;
  attr X is empty
  means :: XBOOLE_0:def 1
  not ex x st x in X;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\noindent Let us observe there is an empty set.

\switchcolumn

\begin{mizar}
registration
  cluster empty for set;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

We now define several new constructions. The constant\index{$\emptyset$} $\emptyset$ is a set, and is
defined by
\begin{definition}
\emptyset\is\ \THE \mathrm{empty}\ \mathrm{set}.
\end{definition}
%\[\IT \is\ \THE \mathrm{empty}\ \mathrm{set} \]

\switchcolumn

\begin{mizar}
definition
  func {} -> set
  equals :: XBOOLE_0:def 2
  the empty set;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}\noindent%
Let $X$, $Y$ be sets. The functor\index{$X\cup Y$}
\[ X\cup Y,\]
with values of type set, is defined by
\begin{definition}
x\in X\cup Y \iff x\in X\lor x\in Y. 
\end{definition}
We can verify this functor is commutative and idempotent.
%\[ x\in\IT \iff x\in X\lor x\in Y. \]

\switchcolumn

\begin{mizar}
  let X,Y be set;
  func X \/ Y -> set
  means :: XBOOLE_0:def 3
  for x holds x in it
  iff x in X or x in Y;
  commutativity;
  idempotence;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}\sloppy\noindent%
The functor\index{$X\cap Y$}
\[ X\cap Y,\]
with values of type set, is defined by
\begin{definition}
x\in X\cap Y \iff x\in X\land x\in Y.
\end{definition}
We can verify this functor is commutative and idempotent.
%\[ x\in\IT \iff x\in X\land x\in Y. \]

\switchcolumn

\begin{mizar}
  func X /\ Y -> set
  means :: XBOOLE_0:def 4
  for x holds x in it
  iff x in X & x in Y;
  commutativity;
  idempotence;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}\noindent%
The functor\index{$X\setminus Y$}
\[ X\setminus Y,\]
with values of type set, is defined by
\begin{definition}
x\in X\setminus Y\iff x\in X\land\ \neg x\in Y. 
\end{definition}
%\[ x\in\IT \iff x\in X\land\ \neg x\in Y. \]

\switchcolumn

\begin{mizar}
  func X \ Y -> set
  means :: XBOOLE_0:def 5
  for x holds x in it
  iff x in X & not x in Y;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let $X$, $Y$ be sets. The functor\index{$X\dotminus Y$}
\[ X\dotminus Y\]
yields a set and is defined by
\begin{definition}
X\dotminus Y = (X \setminus Y)\cup(Y \setminus X).
\end{definition}
We can verify this functor is commutative.
%\[ \IT = (X \setminus Y)\cup(Y \setminus X).\]

\switchcolumn
\begin{mizar}
definition
  let X, Y be set;
  func X \+\ Y -> set
  equals :: XBOOLE_0:def 6
  (X \ Y) \/ (Y \ X);
  commutativity;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
\noindent The predicate\index{Misses}
\[ X\misses Y\]
is defined by
\begin{definition}
X\cap Y = \emptyset. %\forall x\holds x\in X\implies\ \neg x\in Y.
\end{definition}
We can verify this predicate is symmetric.
%\[\forall x\holds x\in X\implies\ \neg x\in Y.\]
\switchcolumn
\begin{mizar}
  pred X misses Y
  means :: XBOOLE_0:def 7
  X /\ Y = {};
  symmetry;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
\noindent The predicate\index{$X \propersubset Y$}
\[ X \propersubset Y \]
is defined by
\begin{definition}
X\subset Y\land X\neq Y.
\end{definition}
We can verify this predicate is irreflexive and antisymmetric.
% \[ X\subset Y\land X\neq Y.\]
\switchcolumn
\begin{mizar}
  pred X c< Y
  means :: XBOOLE_0:def 8
  X c= Y & X <> Y;
  irreflexivity;
  asymmetry;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
\noindent The predicate\index{$\areComparable{X}{Y}$}
\[\areComparable{X}{Y}\]
means
\begin{definition}
X\subset Y\lor Y\subset X.
\end{definition}
We can verify this predicate is reflexive and symmetric.
%\[X\subset Y\lor Y\subset X.\]
\switchcolumn
\begin{mizar}
  pred X,Y are_c=-comparable
  means :: XBOOLE_0:def 9
  X c= Y or Y c= X;
  reflexivity;
  symmetry;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
\noindent Let us note we can characterize the predicate
\[ X = Y \]
by the equivalent condition
\begin{definition}
X\subset Y\land Y\subset X.
\end{definition}
%\[ X\subset Y\land Y\subset X.\]
\switchcolumn
\begin{mizar}
  redefine pred X = Y
  means :: XBOOLE_0:def 10
  X c= Y & Y c= X;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let $X$ and $Y$ be sets. We introduce the notation\index{$X\meets Y$} $X\meets Y$ as an
antonym for $X\misses Y$.
\switchcolumn
\begin{mizar}
notation
  let X, Y be set;
  antonym X meets Y for X misses Y;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
We now state couple of propositions:
\begin{theorem}
x\in X\dotminus Y \iff\ \neg(x\in X\iff x\in Y).
\end{theorem}
\begin{theorem+}
(\forall x\holds\ \neg x\in X\iff (x\in Y\iff x\in Z))\\
\implies X=Y\dotminus Z
\end{theorem+}
\switchcolumn
\begin{mizar}
theorem :: XBOOLE_0:1
 x in X \+\ Y
 iff not (x in X iff x in Y);
theorem :: XBOOLE_0:2
 (for x holds not x in X
              iff (x in Y iff x in Z))
  implies X = Y \+\ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let us observe $\emptyset$ is empty.
\switchcolumn
\begin{mizar}
registration
  cluster {} -> empty;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let $x$ be an arbitrary object. Let us observe $\{x\}$ is nonempty.
Further, let $y$ be an arbitrary object. We also observe $\{x,y\}$ is
nonempty. 
\switchcolumn
\begin{mizar}
registration
  let x;
  cluster { x } -> non empty;
  let y;
  cluster { x, y } -> non empty;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
We observe there exists a non-empty set.
\switchcolumn
\begin{mizar}
registration
  cluster non empty for set;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let $D$ be a non-empty set and let $X$ be an arbitrary set.
We observe $D\cup X$ is non-empty, and that $X\cup D$ is non-empty.
\switchcolumn
\begin{mizar}
registration
  let D be non empty set, X be set;
  cluster D \/ X -> non empty;
  cluster X \/ D -> non empty;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
We will now state several propositions.
\begin{theorem}
X\meets Y\iff\ \ex x\st x\in X\land x\in Y.
\end{theorem}
\begin{theorem}
X\meets Y\iff\ \ex x\st x\in X\cap Y.
\end{theorem}
\begin{theorem+}
X\misses Y\land x\in X\cup Y \implies\\
x\in X\land x\notin Y\;\;\lor\;\; x\in Y\land x\notin X.
\end{theorem+}
\switchcolumn
\begin{mizar}
theorem :: XBOOLE_0:3
  X meets Y iff ex x st x in X & x in Y;

theorem :: XBOOLE_0:4
  X meets Y iff ex x st x in X /\ Y;

theorem :: XBOOLE_0:5
  X misses Y & x in X \/ Y implies
          x in X & not x in Y
          or x in Y & not x in X;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
The scheme \textit{Extensionality} deals with sets $\mathcal{X}$ and
$\mathcal{Y}$ and a unary predicate $\mathcal{P}$ and states that:
\begin{scheme}
\mathcal{X} = \mathcal{Y}
\end{scheme}
provided
\begin{itemize}
\item $\forall x\holds x\in\mathcal{X}\iff\mathcal{P}[x]$ and
\item $\forall x\holds x\in\mathcal{Y}\iff\mathcal{P}[x]$.
\end{itemize}
\switchcolumn
\begin{mizar}
scheme :: XBOOLE_0:sch 2
  Extensionality { X,Y() -> set,
                   P[object] } :
  X() = Y()
provided
 for x holds x in X() iff P[x] and
 for x holds x in Y() iff P[x];
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
The scheme \textit{SetEq}\index{SetEq Scheme} deals with a unary predicate $\mathcal{P}$ and
states
\begin{scheme+}
\forall X_{1}, X_{2}\being\ \mbox{sets}\\
\st(\forall x\holds x\in X_{1}\iff\mathcal{P}[x])\land\\
   (\forall x\holds x\in X_{2}\iff\mathcal{P}[x])\\
\holds X_{1} = X_{2}.
\end{scheme+}
\switchcolumn
\begin{mizar}
scheme :: XBOOLE_0:sch 3
  SetEq { P[object] } :
  for X1,X2 being set
  st (for x holds x in X1 iff P[x])
   & (for x holds x in X2 iff P[x])
  holds X1 = X2;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Now we shall state several more theorems.
\begin{theorem}
X\propersubset Y\implies\ \ex x\st x\in Y\land x\notin X.
\end{theorem}
\begin{theorem}
X\neq\emptyset\implies\ \ex x\st x\in X
\end{theorem}
\begin{theorem}
X\propersubset Y\implies\ \ex x\st x\in Y\land X\subset Y\setminus\{x\}.
\end{theorem}
\switchcolumn
\begin{mizar}
theorem :: XBOOLE_0:6
  X c< Y implies ex x st x in Y
                        & not x in X;
theorem :: XBOOLE_0:7
  X <> {} implies ex x st x in X;
theorem :: XBOOLE_0:8
 X c< Y implies ex x st x in Y 
                      & X c= Y \ {x};
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let $x$, $y$ be arbitrary sets.
We adopt the notation\index{$x\nsubseteq y$} $x\nsubseteq y$ as the antonym for $x\subset y$.
\switchcolumn
\begin{mizar}
notation
  let x,y be set;
  antonym x c/= y for x c= y;
end;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}
Let $x$ be an arbitrary object and let $y$ be a set.
We shall adopt the notation\index{$x\notin y$} $x\notin y$ as the antonym for $x\in y$.
\switchcolumn
\begin{mizar}
notation
  let x be object,y be set;
  antonym x nin y for x in y;
end;
\end{mizar}
\end{paracol}

\bigbreak
\section{Boolean Properties of Sets: Theorems}
\bigbreak

\begin{paracol}{2}
Let $x$, $A$, $B$, $X$, $X_{9}$, $Y$, $Y_{9}$, $Z$, $V$ denote a set.

\switchcolumn

\begin{mizar}
reserve x,A,B,X,X9,Y,Y9,Z,V for set
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \subset Y \land Y \subset Z \implies X \subset Z.
\end{theorem}

\begin{theorem}
  \emptyset \subset X.
\end{theorem}

\begin{theorem}
  X \subset \emptyset \implies X = \emptyset.
\end{theorem}

\begin{theorem}
  (X \cup Y) \cup Z = X \cup (Y \cup Z).
\end{theorem}

\begin{theorem}
  (X \cup Y) \cup Z = (X \cup Z) \cup (Y \cup Z).
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:1
  X c= Y & Y c= Z implies X c= Z;

theorem :: XBOOLE_1:2
  {} c= X;

theorem :: XBOOLE_1:3
  X c= {} implies X = {};

theorem :: XBOOLE_1:4
  (X \/ Y) \/ Z = X \/ (Y \/ Z);

theorem :: XBOOLE_1:5
  (X \/ Y) \/ Z = (X \/ Z) \/ (Y \/ Z);
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cup (X \cup Y) = X \cup Y.
\end{theorem}

\begin{theorem}
  X \subset X \cup Y.
\end{theorem}

\begin{theorem}
  X \subset Z \& Y \subset Z \implies X \cup Y \subset Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies X \cup Z \subset Y \cup Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies X \subset Z \cup Y.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:6
  X \/ (X \/ Y) = X \/ Y;

theorem :: XBOOLE_1:7
  X c= X \/ Y;

theorem :: XBOOLE_1:8
  X c= Z & Y c= Z implies X \/ Y c= Z;

theorem :: XBOOLE_1:9
  X c= Y implies X \/ Z c= Y \/ Z;

theorem :: XBOOLE_1:10
  X c= Y implies X c= Z \/ Y;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cup Y \subset Z \implies X \subset Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies X \cup Y = Y.
\end{theorem}

\begin{theorem}
  X \subset Y \land Z \subset V \implies X \cup Z \subset Y \cup V.
\end{theorem}

\begin{theorem+}
  (Y \subset X \land Z \subset X \land\\
  \forall V \st Y \subset V \land Z \subset V
  \holds X \subset V) \\
  \implies X = Y \cup Z.
\end{theorem+}

\begin{theorem}
  X \cup Y = \emptyset \implies X = \emptyset.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:11
  X \/ Y c= Z implies X c= Z;

theorem :: XBOOLE_1:12
  X c= Y implies X \/ Y = Y;

theorem :: XBOOLE_1:13
  X c= Y & Z c= V implies
  X \/ Z c= Y \/ V;

theorem :: XBOOLE_1:14
  (Y c= X & Z c= X &
   for V st Y c= V & Z c= V
   holds X c= V)
  implies X = Y \/ Z;

theorem :: XBOOLE_1:15
  X \/ Y = {} implies X = {};
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  (X \cap Y) \cap Z = X \cap (Y \cap Z).
\end{theorem}

\begin{theorem}
  X \cap Y \subset X.
\end{theorem}

\begin{theorem}
  X \subset Y \cap Z \implies X \subset Y.
\end{theorem}

\begin{theorem}
  Z \subset X \land Z \subset Y \implies Z \subset X \cap Y.
\end{theorem}

\begin{theorem+}
  (X \subset Y \land X \subset Z \land\\
  \forall V \st V \subset Y \land V \subset Z \holds V \subset X)\\
  \implies X = Y \cap Z.
\end{theorem+}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:16
  (X /\ Y) /\ Z = X /\ (Y /\ Z);

theorem :: XBOOLE_1:17
  X /\ Y c= X;

theorem :: XBOOLE_1:18
  X c= Y /\ Z implies X c= Y;

theorem :: XBOOLE_1:19
  Z c= X & Z c= Y implies Z c= X /\ Y;

theorem :: XBOOLE_1:20
  (X c= Y & X c= Z &
   for V st V c= Y & V c= Z 
   holds V c= X)
  implies X = Y /\ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cap (X \cup Y) = X.
\end{theorem}

\begin{theorem}
  X \cup (X \cap Y) = X.
\end{theorem}

\begin{theorem}
  X \cap (Y \cup Z) = X \cap Y \cup X \cap Z.
\end{theorem}

\begin{theorem}
  X \cup Y \cap Z = (X \cup Y) \cap (X \cup Z).
\end{theorem}

\begin{theorem+}
  (X \cap Y) \cup (Y \cap Z) \cup (Z \cap X)\\
  = (X \cup Y) \cap (Y \cup Z) \cap (Z \cup X).
\end{theorem+}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:21
  X /\ (X \/ Y) = X;

theorem :: XBOOLE_1:22
  X \/ (X /\ Y) = X;

theorem :: XBOOLE_1:23
  X /\ (Y \/ Z) = X /\ Y \/ X /\ Z;

theorem :: XBOOLE_1:24
  X \/ Y /\ Z = (X \/ Y) /\ (X \/ Z);

theorem :: XBOOLE_1:25
  (X /\ Y) \/ (Y /\ Z) \/ (Z /\ X)
  = (X \/ Y) /\ (Y \/ Z) /\ (Z \/ X);
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \subset Y \implies X \cap Z \subset Y \cap Z.
\end{theorem}

\begin{theorem}
  X \subset Y \land Z \subset V \implies X \cap Z \subset Y \cap V.
\end{theorem}

\begin{theorem}
  X \subset Y \implies X \cap Y = X.
\end{theorem}

\begin{theorem}
  X \cap Y \subset X \cup Z.
\end{theorem}

\begin{theorem}
  X \subset Z \implies X \cup Y \cap Z = (X \cup Y) \cap Z.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:26
  X c= Y implies X /\ Z c= Y /\ Z;

theorem :: XBOOLE_1:27
  X c= Y & Z c= V implies 
  X /\ Z c= Y /\ V;

theorem :: XBOOLE_1:28
  X c= Y implies X /\ Y = X;

theorem :: XBOOLE_1:29
  X /\ Y c= X \/ Z;

theorem :: XBOOLE_1:30
  X c= Z implies 
  X \/ Y /\ Z = (X \/ Y) /\ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  (X \cap Y) \cup (X \cap Z) \subset Y \cup Z.
\end{theorem}

\begin{theorem}
  X \setminus Y = Y \setminus X \implies X = Y.
\end{theorem}

\begin{theorem}
  X \subset Y \implies X \setminus Z \subset Y \setminus Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies Z \setminus Y \subset Z \setminus X.
\end{theorem}

\begin{theorem}
  X \subset Y \land Z \subset V \implies X \setminus V \subset Y \setminus Z.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:31
  (X /\ Y) \/ (X /\ Z) c= Y \/ Z;

theorem :: XBOOLE_1:32
  X \ Y = Y \ X implies X = Y;

theorem :: XBOOLE_1:33
  X c= Y implies X \ Z c= Y \ Z;

theorem :: XBOOLE_1:34
  X c= Y implies Z \ Y c= Z \ X;

theorem :: XBOOLE_1:35
  X c= Y & Z c= V 
  implies X \ V c= Y \ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \setminus Y \subset X.
\end{theorem}

\begin{theorem}
  X \setminus Y = \emptyset \iff X \subset Y.
\end{theorem}

\begin{theorem}
  X \subset Y \setminus X \implies X = \emptyset.
\end{theorem}

\begin{theorem}
  X \cup (Y \setminus X) = X \cup Y.
\end{theorem}

\begin{theorem}
  (X \cup Y) \setminus Y = X \setminus Y.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:36
  X \ Y c= X;

theorem :: XBOOLE_1:37
  X \ Y = {} iff X c= Y;

theorem :: XBOOLE_1:38
  X c= Y \ X implies X = {};

theorem :: XBOOLE_1:39
  X \/ (Y \ X) = X \/ Y;

theorem :: XBOOLE_1:40
  (X \/ Y) \ Y = X \ Y;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  (X \setminus Y) \setminus Z = X \setminus (Y \cup Z).
\end{theorem}

\begin{theorem}
  (X \cup Y) \setminus Z = (X \setminus Z) \cup (Y \setminus Z).
\end{theorem}

\begin{theorem}
  X \subset Y \cup Z \implies X \setminus Y \subset Z.
\end{theorem}

\begin{theorem}
  X \setminus Y \subset Z \implies X \subset Y \cup Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies Y = X \cup (Y \setminus X).
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:41
  (X \ Y) \ Z = X \ (Y \/ Z);

theorem :: XBOOLE_1:42
  (X \/ Y) \ Z = (X \ Z) \/ (Y \ Z);

theorem :: XBOOLE_1:43
  X c= Y \/ Z implies X \ Y c= Z;

theorem :: XBOOLE_1:44
  X \ Y c= Z implies X c= Y \/ Z;

theorem :: XBOOLE_1:45
  X c= Y implies Y = X \/ (Y \ X);
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \setminus (X \cup Y) = \emptyset.
\end{theorem}

\begin{theorem}
  X \setminus X \cap Y = X \setminus Y.
\end{theorem}

\begin{theorem}
  X \setminus (X \setminus Y) = X \cap Y.
\end{theorem}

\begin{theorem}
  X \cap (Y \setminus Z) = (X \cap Y) \setminus Z.
\end{theorem}

\begin{theorem}
  X \cap (Y \setminus Z) = X \cap Y \setminus X \cap Z.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:46
  X \ (X \/ Y) = {};

theorem :: XBOOLE_1:47
  X \ X /\ Y = X \ Y;

theorem :: XBOOLE_1:48
  X \ (X \ Y) = X /\ Y;

theorem :: XBOOLE_1:49
  X /\ (Y \ Z) = (X /\ Y) \ Z;

theorem :: XBOOLE_1:50
  X /\ (Y \ Z) = X /\ Y \ X /\ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cap Y \cup (X \setminus Y) = X.
\end{theorem}

\begin{theorem}
  X \setminus (Y \setminus Z) = (X \setminus Y) \cup X \cap Z.
\end{theorem}

\begin{theorem}
  X \setminus (Y \cup Z) = (X \setminus Y) \cap (X \setminus Z).
\end{theorem}

\begin{theorem}
  X \setminus (Y \cap Z) = (X \setminus Y) \cup (X \setminus Z).
\end{theorem}

\begin{theorem}
  (X \cup Y) \setminus (X \cap Y) = (X \setminus Y) \cup (Y \setminus X).
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:51
  X /\ Y \/ (X \ Y) = X;

theorem :: XBOOLE_1:52
  X \ (Y \ Z) = (X \ Y) \/ X /\ Z;

theorem :: XBOOLE_1:53
  X \ (Y \/ Z) = (X \ Y) /\ (X \ Z);

theorem :: XBOOLE_1:54
  X \ (Y /\ Z) = (X \ Y) \/ (X \ Z);

theorem :: XBOOLE_1:55
  (X \/ Y) \ (X /\ Y) 
  = (X \ Y) \/ (Y \ X);
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \propersubset Y \land Y \propersubset Z \implies X \propersubset Z.
\end{theorem}

\begin{theorem}
  \neg (X \propersubset Y \land Y \propersubset X).
\end{theorem}

\begin{theorem}
  X \propersubset Y \land Y \subset Z \implies X \propersubset Z.
\end{theorem}

\begin{theorem}
  X \subset Y \land Y \propersubset Z \implies X \propersubset Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies Y \not\propersubset X.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:56
  X c< Y & Y c< Z implies X c< Z;

theorem :: XBOOLE_1:57
  not (X c< Y & Y c< X);

theorem :: XBOOLE_1:58
  X c< Y & Y c= Z implies X c< Z;

theorem :: XBOOLE_1:59
  X c= Y & Y c< Z implies X c< Z;

theorem :: XBOOLE_1:60
  X c= Y implies not Y c< X;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \neq \emptyset \implies \emptyset \propersubset X.
\end{theorem}

\begin{theorem}
  X \not\propersubset \emptyset.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:61
  X <> {} implies {} c< X;

theorem :: XBOOLE_1:62
  not X c< {};
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \subset Y \land Y \misses Z
  \implies X \misses Z.
\end{theorem}

\begin{theorem+}
  A \subset X \land B \subset Y \land X \misses Y\\
  \implies A \misses B.
\end{theorem+}

\begin{theorem}
  X \misses \emptyset.
\end{theorem}

\begin{theorem}
  X \meets X \iff X \neq \emptyset.
\end{theorem}

\begin{theorem+}
  X \subset Y \land X \subset Z \land Y \misses Z\\
  \implies X = \emptyset.
\end{theorem+}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:63
  X c= Y & Y misses Z 
  implies X misses Z;

theorem :: XBOOLE_1:64
  A c= X & B c= Y & X misses Y 
  implies A misses B;

theorem :: XBOOLE_1:65
  X misses {};

theorem :: XBOOLE_1:66
  X meets X iff X <> {};

theorem :: XBOOLE_1:67
  X c= Y & X c= Z & Y misses Z 
  implies X = {};
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem+}
  \forall A \being \mbox{non empty set}\\
  \st A \subset Y \land A \subset Z\\
  \holds Y \meets Z.
\end{theorem+}

\begin{theorem+}
  \forall A \being \mbox{non empty set} \st A \subset Y\\
  \holds A\, \meets\, Y.
\end{theorem+}

\begin{theorem}
  X \meets Y \cup Z \iff X \meets Y \lor X \meets Z.
\end{theorem}

\begin{theorem+}
  X \cup Y = Z \cup Y \land X \misses Y \land Z \misses Y\\
  \implies X = Z.
\end{theorem+}

\begin{theorem+}
  X_{9} \cup Y_{9} = X \cup Y \land\\
  X \misses X_{9} \land Y \misses Y_{9}\\
  \implies X = Y_{9}.
\end{theorem+}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:68
  for A being non empty set 
  st A c= Y & A c= Z 
  holds Y meets Z;

theorem :: XBOOLE_1:69
  for A being non empty set st A c= Y 
  holds A meets Y;

theorem :: XBOOLE_1:70
  X meets Y \/ Z 
  iff X meets Y or X meets Z;

theorem :: XBOOLE_1:71
  X \/ Y = Z \/ Y & X misses Y & Z misses Y 
  implies X = Z;

theorem :: XBOOLE_1:72
  X9 \/ Y9 = X \/ Y & X misses X9 & Y misses Y9 
  implies X = Y9;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \subset Y \cup Z \land X \misses Z \implies X \subset Y.
\end{theorem}

\begin{theorem}
  X \meets Y \cap Z \implies X \meets Y.
\end{theorem}

\begin{theorem}
  X \meets Y \implies X \cap Y \meets Y.
\end{theorem}

\begin{theorem}
  Y \misses Z \implies X \cap Y \misses X \cap Z.
\end{theorem}

\begin{theorem}
  X \meets Y \land X \subset Z \implies X \meets Y \cap Z.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:73
  X c= Y \/ Z & X misses Z implies X c= Y;

theorem :: XBOOLE_1:74
  X meets Y /\ Z implies X meets Y;

theorem :: XBOOLE_1:75
  X meets Y implies X /\ Y meets Y;

theorem :: XBOOLE_1:76
  Y misses Z implies X /\ Y misses X /\ Z;

theorem :: XBOOLE_1:77
  X meets Y & X c= Z implies X meets Y /\ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \misses Y \implies X \cap (Y \cup Z) = X \cap Z.
\end{theorem}

\begin{theorem}
  X \setminus Y \misses Y.
\end{theorem}

\begin{theorem}
  X \misses Y \implies X \misses Y \setminus Z.
\end{theorem}

\begin{theorem}
  X \misses Y \setminus Z \implies Y \misses X \setminus Z.
\end{theorem}

\begin{theorem}
  X \setminus Y \misses Y \setminus X.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:78
  X misses Y implies X /\ (Y \/ Z) = X /\ Z;

theorem :: XBOOLE_1:79
  X \ Y misses Y;

theorem :: XBOOLE_1:80
  X misses Y implies X misses Y \ Z;

theorem :: XBOOLE_1:81
  X misses Y \ Z implies Y misses X \ Z;

theorem :: XBOOLE_1:82
  X \ Y misses Y \ X;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \misses Y \iff X \setminus Y = X.
\end{theorem}

\begin{theorem}
  X \meets Y \land X \misses Z \implies X \meets Y \setminus Z.
\end{theorem}

\begin{theorem}
  X \subset Y \implies X \misses Z \setminus Y.
\end{theorem}

\begin{theorem}
  X \subset Y \land X \misses Z \implies X \subset Y \setminus Z.
\end{theorem}

\begin{theorem}
  Y \misses Z \implies (X \setminus Y) \cup Z = (X \cup Z) \setminus Y.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:83
  X misses Y iff X \ Y = X;

theorem :: XBOOLE_1:84
  X meets Y & X misses Z 
  implies X meets Y \ Z;

theorem :: XBOOLE_1:85
  X c= Y implies X misses Z \ Y;

theorem :: XBOOLE_1:86
  X c= Y & X misses Z 
  implies X c= Y \ Z;

theorem :: XBOOLE_1:87
  Y misses Z 
  implies (X \ Y) \/ Z = (X \/ Z) \ Y;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \misses Y \implies (X \cup Y) \setminus Y = X.
\end{theorem}

\begin{theorem}
  X \cap Y \misses X \setminus Y.
\end{theorem}

\begin{theorem}
  X \setminus (X \cap Y) \misses Y.
\end{theorem}

\begin{theorem}
  (X \dotminus Y) \dotminus Z = X \dotminus (Y \dotminus Z).
\end{theorem}

\begin{theorem}
  X \dotminus X = \emptyset.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:88
  X misses Y implies (X \/ Y) \ Y = X;

theorem :: XBOOLE_1:89
  X /\ Y misses X \ Y;

theorem :: XBOOLE_1:90
  X \ (X /\ Y) misses Y;

theorem :: XBOOLE_1:91
  (X \+\ Y) \+\ Z = X \+\ (Y \+\ Z);

theorem :: XBOOLE_1:92
  X \+\ X = {};
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cup Y = (X \dotminus Y) \cup X \cap Y.
\end{theorem}

\begin{theorem}
  X \cup Y = X \dotminus Y \dotminus X \cap Y.
\end{theorem}

\begin{theorem}
  X \cap Y = X \dotminus Y \dotminus (X \cup Y).
\end{theorem}

\begin{theorem}
  X \setminus Y \subset X \dotminus Y.
\end{theorem}

\begin{theorem}
  X \setminus Y \subset Z \land Y \setminus X \subset Z \implies X \dotminus Y \subset Z.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:93
  X \/ Y = (X \+\ Y) \/ X /\ Y;

theorem :: XBOOLE_1:94
  X \/ Y = X \+\ Y \+\ X /\ Y;

theorem :: XBOOLE_1:95
  X /\ Y = X \+\ Y \+\ (X \/ Y);

theorem :: XBOOLE_1:96
  X \ Y c= X \+\ Y;

theorem :: XBOOLE_1:97
  X \ Y c= Z & Y \ X c= Z 
  implies X \+\ Y c= Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cup Y = X \dotminus (Y \setminus X).
\end{theorem}

\begin{theorem}
  (X \dotminus Y) \setminus Z = (X \setminus (Y \cup Z)) \cup (Y \setminus (X \cup Z)).
\end{theorem}

\begin{theorem}
  X \setminus Y = X \dotminus (X \cap Y).
\end{theorem}

\begin{theorem}
  X \dotminus Y = (X \cup Y) \setminus X \cap Y.
\end{theorem}

\begin{theorem}
  X \setminus (Y \dotminus Z) = X \setminus (Y \cup Z) \cup X \cap Y \cap Z.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:98
  X \/ Y = X \+\ (Y \ X);

theorem :: XBOOLE_1:99
  (X \+\ Y) \ Z
  = (X \ (Y \/ Z)) \/ (Y \ (X \/ Z));

theorem :: XBOOLE_1:100
  X \ Y = X \+\ (X /\ Y);

theorem :: XBOOLE_1:101
  X \+\ Y = (X \/ Y) \ X /\ Y;

theorem :: XBOOLE_1:102
  X \ (Y \+\ Z) 
  = X \ (Y \/ Z) \/ X /\ Y /\ Z;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \cap Y \misses X \dotminus Y.
\end{theorem}

\begin{theorem+}
  X \propersubset Y \lor X = Y \lor Y \propersubset X\\
  \iff \areComparable{X}{Y}.
\end{theorem+}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:103
  X /\ Y misses X \+\ Y;

theorem :: XBOOLE_1:104
  X c< Y or X = Y or Y c< X
  iff X,Y are_c=-comparable;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem+}
  \forall X, Y \being \mbox{set} \st X \propersubset Y\\
  \holds Y \setminus X \neq \emptyset.
\end{theorem+}

\begin{theorem}
  X \subset A \setminus B \implies X \subset A \land X \misses B.
\end{theorem}

\begin{theorem}
  X \subset A \dotminus B \iff X \subset A \cup B \land X \misses A \cap B.
\end{theorem}

\begin{theorem}
  X \subset A \implies X \cap Y \subset A.
\end{theorem}

\begin{theorem}
  X \subset A \implies X \setminus Y \subset A.
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:105
  for X, Y being set st X c< Y
  holds Y \ X <> {};

theorem :: XBOOLE_1:106
  X c= A \ B implies X c= A & X misses B;

theorem :: XBOOLE_1:107
  X c= A \+\ B iff
  X c= A \/ B & X misses A /\ B;

theorem :: XBOOLE_1:108
  X c= A implies X /\ Y c= A;

theorem :: XBOOLE_1:109
  X c= A implies X \ Y c= A;
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem}
  X \subset A \land Y \subset A \implies X \dotminus Y \subset A.
\end{theorem}

\begin{theorem}
  (X \cap Z) \setminus (Y \cap Z) = (X \setminus Y) \cap Z.
\end{theorem}

\begin{theorem}
  (X \cap Z) \dotminus (Y \cap Z) = (X \dotminus Y) \cap Z.
\end{theorem}

\begin{theorem}
  X \cup Y \cup Z \cup V = X \cup (Y \cup Z \cup V).
\end{theorem}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:110
  X c= A & Y c= A implies X \+\ Y c= A;

theorem :: XBOOLE_1:111
  (X /\ Z) \ (Y /\ Z) = (X \ Y) /\ Z;

theorem :: XBOOLE_1:112
  (X /\ Z) \+\ (Y /\ Z) = (X \+\ Y) /\ Z;

theorem :: XBOOLE_1:113
  X \/ Y \/ Z \/ V = X \/ (Y \/ Z \/ V);
\end{mizar}

\switchcolumn*\ensurevspace{5cm}

\begin{theorem+}
  \forall A,B,C,D \being \mbox{set}\\
  \st A \misses D \land B \misses D \land C \misses D\\
  \holds A \cup B \cup C \misses D.
\end{theorem+}

\refstepcounter{theorem} % 115 was cancelled
\begin{theorem}
  X \cap (Y \cap Z) = (X \cap Y) \cap (X \cap Z).
\end{theorem}

\begin{theorem+}
  \forall P,G,C \being \mbox{set}
  \st C \subset G\\
  \holds P \setminus C = (P \setminus G) \cup (P \cap (G \setminus C)).
\end{theorem+}

\switchcolumn

\begin{mizar}
theorem :: XBOOLE_1:114
  for A,B,C,D being set
  st A misses D & B misses D & C misses D
  holds A \/ B \/ C misses D;

theorem :: XBOOLE_1:116
  X /\ (Y /\ Z) = (X /\ Y) /\ (X /\ Z);

theorem :: XBOOLE_1:117
  for P,G,C being set 
  st C c= G 
  holds P \ C = (P \ G) \/ (P /\ (G \ C));
\end{mizar}


\end{paracol}
